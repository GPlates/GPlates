# $Id$
# Configure script for GPlates

AC_INIT([GPlates], [0.001], [http://sourceforge.net/tracker/?func=add&group_id=74515&atid=541217], [gplates])
AC_PREREQ(2.53)
AC_CONFIG_SRCDIR(src/gplates_main.cc)
AC_CONFIG_HEADERS(src/global/config.h)

# Check C++ compiler
# Setting CXXFLAGS is necessary to circumvent autoconf using its
#  own (namely CXXFLAGS="-g -O2").
CXXFLAGS=""
AC_PROG_CXX
AC_PROG_MAKE_SET
CHECK_GNU_MAKE
AC_PROG_INSTALL
AC_LANG_CPLUSPLUS

# What system are we on?
AC_PATH_PROG(unamepath, uname)
if test "_$unamepath" = _; then
	system="unknown"
else
	AC_MSG_CHECKING(system type)
	system=`$unamepath -s`
	AC_MSG_RESULT($system)
fi

##################################
# System-dependant options
##################################
# Debugging format; will be appended to "-g", and passed to the compiler
debug_format=""
if test "$system" = "Linux"; then
	debug_format="stabs+"
elif test "$system" = "SunOS"; then
	debug_format="stabs"
fi

# Check for required features
AC_CXX_BOOL
if test "$ac_cv_cxx_bool" != yes; then
	AC_MSG_ERROR([bool is not a built-in type])
fi
AC_CXX_EXCEPTIONS
if test "$ac_cv_cxx_exceptions" != yes; then
	AC_MSG_ERROR([compiler doesn't support exceptions])
fi
AC_CXX_TEMPLATES
if test "$ac_cv_cxx_templates" != yes; then
	AC_MSG_ERROR([compiler doesn't support templates])
fi
AC_CXX_NAMESPACES
if test "$ac_cv_cxx_namespaces" != yes; then
	AC_MSG_ERROR([compiler doesn't implement namespaces])
fi
AC_CXX_HAVE_IEEE_MATH
if test "$ac_cv_cxx_have_ieee_math" != yes; then
	AC_MSG_ERROR([compiler doesn't support IEEE maths library])
fi
AC_CXX_HAVE_SSTREAM
if test "$ac_cv_cxx_have_sstream" != yes; then
	AC_MSG_ERROR([can't find stringstream])
fi
AC_CXX_HAVE_STD
if test "$ac_cv_cxx_have_std" != yes; then
	AC_MSG_ERROR([no ISO C++ standard library present])
fi
AC_CXX_HAVE_STL
if test "$ac_cv_cxx_have_stl" != yes; then
	AC_MSG_ERROR([no C++ STL present])
fi

# XXX: We don't *really* need this.
#AC_CXX_RTTI
#if test "$ac_cv_cxx_rtti" != yes; then
#	AC_MSG_ERROR([compiler doesn't support RTTI])
#fi


## 
# Library dependencies.
##
AC_CHECK_LIB([GL], [main], [], AC_MSG_ERROR([
!!
!!  You don't appear to have the OpenGL shared library installed.
!!  It may be obtained from
!!     http://www.opengl.org
!!]))

AC_CHECK_LIB([GLU], [main], [], AC_MSG_ERROR([
!!
!!  You don't appear to have the OpenGL Utility Library (GLU) 
!!  shared library installed.  It may be obtained from 
!!     http://www.opengl.org
!!]))

# wxWindows
# ???

# We using Expat, which is included in contrib/src
# XXX: Only compile eXpat if it does not already exist?
# XXX: For now, place the obligation of eXpat installation
#  on the user.
AC_CHECK_LIB([expat], [main], [], AC_MSG_ERROR([
!!
!!  You don't appear to have the eXpat XML Parser 
!!  shared library installed.  It may be obtained from 
!!     http://expat.sourceforge.net
!!]))


##
# Header dependencies.
##

# OpenGL and GLU
AC_CHECK_HEADERS([GL/gl.h GL/glu.h], [], AC_MSG_ERROR([
!!
!!  Failed to find the OpenGL headers.  Make sure the OpenGL
!!  development package is installed on your system.
!!]))

# wxWindows
# ???

# eXpat XML Parser
# XXX: I had to alter my local copy of expat.h because it makes
#  forward references to enums which gcc-2.95 doesn't like.
AC_CHECK_HEADERS([expat.h], [], AC_MSG_ERROR([
!!
!!  Failed to find the eXpat headers.  Make sure the eXpat
!!  development package is installed on your system.
!!]))


# Check for dot (from graphviz package) for doxygen.conf
# WARNING: does not verify this is the _real_ dot program we want
AC_CHECK_PROG([HAVE_DOT], [dot], [YES], [NO])


EXTRA_CFLAGS=""

# Using '--enable-dev' changes compile-time flags to be more strict
turn_on_dev="no"
AC_ARG_ENABLE(dev, [  --enable-dev            Enable developer flags],
[
	turn_on_dev="yes"
	EXTRA_CFLAGS="$EXTRA_CFLAGS -Wall -pedantic -ansi -W -Wshadow -Wcast-align -Wwrite-strings -Wfloat-equal -Wno-unused-parameter -Wctor-dtor-privacy -Wnon-virtual-dtor -Wpointer-arith -Werror -g$debug_format"
# This warning breaks wxWindows: -Wold-style-cast 
])

# Using '--enable-opt' turns on optimisations
turn_on_opt="no"
AC_ARG_ENABLE(opt, [  --enable-opt            Enable optimisation flags],
[
	turn_on_opt="yes"
	EXTRA_CFLAGS="$EXTRA_CFLAGS -fshort-enums -fstrength-reduce"
])

# Using '--enable-prof' enables profiling flags
turn_on_prof="no"
AC_ARG_ENABLE(prof, [  --enable-prof           Enable profiling],
[
	turn_on_prof="yes"
	EXTRA_CFLAGS="$EXTRA_CFLAGS -g -pg"
	EXTRA_LDFLAGS="-g -pg"
])


AC_MSG_CHECKING([for Plesiosaurs in Loch Ness])
AC_MSG_RESULT(yes)


##################################
# UI targets
##################################
probe_testui="auto"
probe_wxwin="auto"

AC_ARG_ENABLE(testui,
	[  --disable-testui        Disable Test UI],
	[probe_testui=$enableval])
AC_ARG_ENABLE(wxwin,
	[  --disable-wxwin         Disable probing for wxWindows],
	[probe_wxwin=$enableval])


any_ui="no"
UI_TARGETS=""
if test "$probe_testui" != "no"; then
	# FIXME: Anything to test for?

	enable_testui="yes"
	AC_DEFINE(HAVE_TESTUI)
	#UI_TARGETS="$UI_TARGETS ui-test.o"	# XXX
	#LIBS="$LIBS -l???"			# XXX
	any_ui="yes"
fi
if test "$probe_wxwin" != "no"; then
	# Check for wxWindows

	if test -z "$WX_CONFIG"; then
		AC_PATH_PROG(WX_CONFIG, wx-config, no)
	fi
	if test "$WX_CONFIG" = "no"; then
		echo "### ERROR: The wxWindows configuration tool could not be found."
		echo "###  Make sure that you have wxWindows installed, and that"
		echo "###  wx-config is in your PATH or is referred to by the WX_CONFIG"
		echo "###  shell variable."
		enable_wxwin="no"
	else
		echo -n "Determining wxWindows-specific compilation flags... "
		WXWIN_CFLAGS=`$WX_CONFIG --cflags`
		WXWIN_LIBS=`$WX_CONFIG --libs`
		WXWIN_GL_LIBS=`$WX_CONFIG --gl-libs`
		echo "done."
		enable_wxwin="yes"
	fi
	
	if test "$enable_wxwin" = "yes"; then
		# PKG_CHECK_MODULES has set up:
		#	- WXWIN_CFLAGS
		#	- WXWIN_LIBS
		#	- WXWIN_GL_LIBS

		AC_DEFINE(HAVE_WXWIN)
		#UI_TARGETS="$UI_TARGETS ui-gtkmm.o"	# XXX
		EXTRA_CFLAGS="$EXTRA_CFLAGS $WXWIN_CFLAGS"
		LIBS="$LIBS $WXWIN_LIBS $WXWIN_GL_LIBS"
		any_ui="yes"
	fi
fi

AC_SUBST(EXTRA_CFLAGS)
AC_SUBST(EXTRA_LDFLAGS)


# Fail if no UIs found
if test "$any_ui" = "no"; then
	echo "#############################"
	echo "# ERROR: No UIs have been   #"
	echo "#  configured! At least one #"
	echo "#  driver must be present   #"
	echo "#  for GPlates to function. #"
	echo "#############################"
	exit 1
fi


# Write output
AC_OUTPUT(Makefile
	doc/doxygen.conf
	doc/Makefile
	src/Rules
	src/Makefile
	src/fileio/Makefile
	src/geo/Makefile
	src/global/Makefile
	src/gui/Makefile
	src/maths/Makefile)



# Some warnings
if test "$turn_on_dev" = "yes"; then
	echo "###"
	echo "### WARNING: You have enabled Developer flags. Unless you"
	echo "###          are a developer, you are strongly advised to"
	echo "###          leave developer flags turned off, or you may"
	echo "###          have problems compiling GPlates."
	echo "###"
fi
if test "$turn_on_prof" = "yes"; then
	echo "###"
	echo "### WARNING: You have enabled Profiling. Unless you are a"
	echo "###          developer, you are strongly advised to leave"
	echo "###          profiling turned off, or you may have problems"
	echo "###          compiling or running GPlates."
	echo "###"
fi
if test "$turn_on_opt" = "yes" -a "$turn_on_dev" = "yes"; then
	echo "###"
	echo "### WARNING: You have enabled both Optimisation and Development"
	echo "###          flags. This is highly discouraged, as it may cause"
	echo "###          problems compiling or running GPlates."
	echo "###"
fi

# UI information
echo "###############################################"
echo "#  User interfaces enabled:                   #"
if test "$enable_testui" = "yes"; then
	echo "#    * TestUI                                 #"; fi
if test "$enable_wxwin" = "yes"; then
	echo "#    * wxWindows                              #"; fi

echo "###############################################"
echo "#  GPlates is now configured. Simply type...  #"
echo "#          make                               #"
echo "#  ... and GPlates will be compiled.          #"
echo "###############################################"
